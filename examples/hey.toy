
fun squared(n) { n * n }
fun sqrt(x) { x ** 0.5 } 

fun isPrime(n) {
    if n < 2 { return false }
    if n == 2 { return true }
    if n mod 2 == 0 { return false }

    mut i = 3

    while squared(i) <= n {
        if n mod i == 0 {
            return false
        }

        i += 2
    }

    true
}

fun primes(count) {
    mut primes = [2]
    mut i = 3

    while len(primes) < count {
        if isPrime(i) {
            primes.append(i)
        }
        
        i += 2
    }

    primes
}

fun sum(elems) {
    mut total = 0

    for n in elems {
        total = total + n
    }

    total
}

fun map(elems, f) {
    mut mapped = []

    for elem in elems {
        mapped.append(f(elem))
    }

    mapped
}

fun filter(elems, f) {
    mut filtered = []

    for elem in elems {
        if f(elem) {
            append(filtered, elem)
        }
    }

    filtered
}

fun any(elems, f) {
    for elem in elems {
        if f(elem) {
            return true
        }
    }

    false
}

fun all(elems, f) {
    for elem in elems {
        if !f(elem) {
            return false
        }
    }

    true
}

fun chars(str) {
    mut characters = []
    mut i = 0
    mut length = str.len()

    while i < length {
        characters.append(str.charAt(i))
        i += 1
    }

    characters
}

fun hash(str, n) {
    mut h = 0
    mut length = str.len()
    mut i = 0

    while i < length {
        h = (h + 31 * str.charCodeAt(i)) mod n
        i += 1
    }

    h
}

fun range(n) {
    mut i = 0
    mut rng = []

    while i < n {
        rng.append(i)
        i += 1
    }

    rng
}

fun findIndex(elems, f) {
    mut i = 0
    let length = elems.len()

    while i < length {
        if f(elems[i]) {
            return i
        }

        i += 1
    }

    -1
}

fun hashMake(n) {
    mut buckets = []
    mut i = 0

    while i < n {
        buckets.append([])
        i += 1
    }

    buckets
}

fun hashKeyLocation(h, key) {
    let n = h.len()
    let idx = key.hash(n)
    mut bucket = h[idx]
    let entryIdx = bucket.findIndex(fun(entry) entry.0 == key)
    (bucket, entryIdx)
}

fun hashSet(h, key, value) {
    let loc = h.hashKeyLocation(key)
    let bucket = loc.0
    let entryIdx = loc.1
    
    if entryIdx >= 0 {
        bucket[entryIdx] = (key, value)
    } else {
        bucket.append((key, value))
    }
}

fun hashGet(h, key) {
    let loc = h.hashKeyLocation(key)
    let bucket = loc.0
    let entryIdx = loc.1

    if entryIdx == -1 {
        panic("hashGet: key '" ++ key ++ "' not found")
    }

    bucket[entryIdx].1
}

fun hashHas(h, key) {
    hashKeyLocation(h, key).1 != -1   
}

fun hashEntries(h) {
    mut entries = []
    
    for bucket in h {
        for entry in bucket {
            entries.append(entry)
        }
    }

    entries
}

fun hashKeys(h) {
    hashEntries(h).map(fun(entry) entry.0)
}

fun hashValues(h) {
    hashEntries(h).map(fun(entry) entry.1)
}

mut population = hashMake(10)
population.hashSet("Gueberschwihr", 838)
population.hashSet("Eguisheim", 1734)
population.hashSet("Colmar", 70284)
population.hashSet("Troyes", 60928)
population.hashSet("Paris", 2102650)
print(population)
